= Neo4Art: Van Gogh's journey in a Neo4j graph DB

== I. Preface 

Vincent Van Gogh is widely considered one of the most important neo-impressionist painters.

One fundamental aspect of his artwork was his continuous research for colours: “_during his early adulthood, while living between The Netherlands and Belgium, his palette consisted mainly of somber earth tones and showed no sign of the vivid coloration that distinguished his later work. In March 1886, he moved to Paris and discovered the French Impressionists. Later, he moved to the south of France and was influenced by the strong sunlight he found there. His work grew brighter in color, and he developed the unique and highly recognizable style that became fully realized during his stay in Arles in 1888_”

So how would I go about putting Van Gogh’s journey into Neo4j?

== II. Introduction

First, I started with the data source: The http://www.vggallery.com/[Vincent Van Gogh Gallery] has a complete list of his paintings.

The root node is of course Vincent Van Gogh himself:

[source,cypher]
----
CREATE (a:ARTIST {first_name: 'Vincent', second_name: 'Willem', last_name: 'Van Gogh', born: 18530330, died: 18900729})
----

Since I’ll access _ARTIST_ nodes through its last_name property, I decided to introduce an unique index constraint:

[source,cypher]
----
CREATE CONSTRAINT ON (a:ARTIST) ASSERT a.last_name IS UNIQUE
----

Now, I’ll focus on _CITIES_ and _STAGES_. First of all, where he was born and where he died:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})
CREATE (a)-[:BORN_IN]->(c:CITY {name: 'Zundert', province: 'North Brabant', country: 'Netherland', latitude: 51.466667, longitude: 4.666667})
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})
CREATE (a)-[:DIED_IN]->(c:CITY {name: 'Auvers-sur-Oise', region: 'Île-de-France', country: 'France', latitude: 49.0725, longitude: 2.175})
----

Please note, we have a _CITY_ with a province property and the other with a region property: Neo4j is *schema-less* (or, more correctly, *“less schema”*, as Jim Webber pointed out on his http://jimwebber.org/2014/01/starting-graph-databases-with-neo4j-2-0/[blog]).

Please also note that I’m saving latitude and longitude informations for future (maybe my next spike) geo-spatial queries: at present, I’m just supposing this is the correct way to store such kind of data.

Vincent Van Gogh adhered to the *‘Post-Impressionism’* art movement (more correctly, one of its founder):

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})
CREATE (a)-[:BELONGS_TO]->(m:ART_MOVEMENT {name: 'Post-Impressionism', wikipedia: 'http://en.wikipedia.org/wiki/Post-Impressionism'})
----

Now I could write the first, very simple Cypher statement to look at nodes and relations I’ve just created:

[source,cypher]
----
MATCH (art_movement:ART_MOVEMENT)<-[:BELONGS_TO]-(artist:ARTIST {last_name: 'Van Gogh'})-[r]->(city:CITY)
RETURN artist, art_movement, city
----

//graph

We can see a graph already emerging! :)


== III. JOURNEY’S STAGES

On December 1883, Van Gogh began his journey by moving to *Neuen*, North Brabant, Netherlands. He lived there until November 1885:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'}) CREATE (a)-[:MOVED_TO]->(s:STAGE {from: 188312, to: 188511})
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:IN]->(c:CITY {name: 'Neuen', province: 'North Brabant', country: 'Netherland', latitude: 51.473333, longitude: 5.546667})
----

As you can see, I’ve used the aggregation function *collect* and the collection function *last*, to retrieve current last stage.

_I’m still a little bit undecided about creating a specific node labeled STAGE (I could have used CITY and relations with properties between cities directly), but stages were really significant for Van Gogh, that’s why, to describe his journey I’ve decided to create a linked list of stages (please, tell me if you think that’s wrong)._

In december 1885, Van Gogh moved to *Antwerp*, Belgium, where he lived until February 1886:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:MOVED_TO]->(:STAGE {from: 188512, to: 188602})
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:IN]->(c:CITY {name: 'Antwerp', region: 'Flemish Region', country: 'Belgium', latitude: 51.216667, longitude: 4.4})
----

Van Gogh lived in *Paris*, France from March 1886 to February 1888:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:MOVED_TO]->(:STAGE {from: 188603, to: 188802 })
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:IN]->(c:CITY {name: 'Paris', region: 'Île-de-France', country: 'France', latitude: 48.8567, longitude: 2.3508})
----

Van Gogh moved to *Arles*, France in February 1888 and stayed there until May 1889:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:MOVED_TO]->(:STAGE {from: 188802, to: 188905 })
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:IN]->(c:CITY {name: 'Arles', region: 'Provence-Alpes-Côte d\'Azur', country: 'France', latitude: 43.6767, longitude: 4.6278})
----

_(Please, pay attention to the back slash in d\’Azur)._

Then he moved to *Saint-Rémy de Provence*, France in May 1889 and stayed there until May 1890:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:MOVED_TO]->(:STAGE {from: 188905, to: 189005 })
----

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:IN]->(c:CITY {name: 'Saint-Rémy de Provence', region: 'Provence-Alpes-Côte d\'Azur', country: 'France', latitude: 43.79, longitude: 4.8325})
----

Van Gogh ended his trip, and also his life by committing suicide, in *Auvers-sur-Oise*, France where he had lived from May to July 1890:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
CREATE (last_stage)-[:MOVED_TO]->(:STAGE {from: 189005, to: 189007 })
----

Auvers-sur-Oise was already created, so I’m going to change a little the usual statement:

[source,cypher]
----
MATCH (a:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)
WITH last(collect(stage)) as last_stage
MATCH (c:CITY {name: 'Auvers-sur-Oise'})
CREATE (last_stage)-[:IN]->(c)
----

Now I’m able to inquiry the system to visualize Van Gogh’s journey:

[source,cypher]
----
MATCH (artist:ARTIST {last_name: 'Van Gogh'})-[:MOVED_TO*]->(stage:STAGE)-[:IN]->(city:CITY)
WITH artist, stage, city
MATCH (artist)-[:BELONGS_TO]->(art_movement:ART_MOVEMENT)
RETURN artist, art_movement, stage, city
----

//graph



